import org.apache.tools.ant.taskdefs.condition.Os

repositories {
    maven { url "https://software.r3.com/artifactory/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

task freighterTest(type: Test, dependsOn: [project(":gallery-workflows").jar]) {
    description = "Runs freighter tests"
    group = "verification"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath

    maxParallelForks Integer.parseInt(project.findProperty("freighter.forks") as String ?: "1")
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"

    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "ORACLE"
        if (Os.isFamily(Os.FAMILY_WINDOWS) || Os.isFamily(Os.FAMILY_MAC)) {
            //windows + mac cannot run comcast tests and cannot run oracle tests due to credential store issues
            excludeTags "FULL_LINUX_KERNEL", "ORACLE"
        }

        if (Os.isFamily(Os.FAMILY_UNIX) && "uname -a".execute().getText().contains("microsoft")) {
            excludeTags "FULL_LINUX_KERNEL"
        }
    }
}

configurations {
    freighterTestCompile.extendsFrom testCompile
    freighterTestRuntime.extendsFrom testRuntime
}

dependencies {
    freighterTestCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    freighterTestCompile "freighter:freighter-testing-core-junit5:$freighterVersion"
    freighterTestCompile "$cordaReleaseGroup:corda-rpc:4.7"

    freighterTestCompile project(":gallery-contracts")
    freighterTestCompile project(":gallery-workflows")
}