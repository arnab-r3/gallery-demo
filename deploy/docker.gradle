import kotlin.Triple
import org.apache.commons.lang3.RandomStringUtils

// Global scope props
rootProject.ext.'corda.artifactory.username' = project.findProperty("corda.artifactory.username")
        ?: System.getenv("CORDA_ARTIFACTORY_USERNAME")

rootProject.ext.'corda.artifactory.password' = project.findProperty("corda.artifactory.password")
        ?: System.getenv("CORDA_ARTIFACTORY_PASSWORD")

rootProject.ext.'docker.push.username' = project.findProperty("docker.push.username")
        ?: System.getenv("DOCKER_PUSH_USERNAME") ?: "r3payments"

rootProject.ext.'docker.push.password' = project.findProperty("docker.push.password")
        ?: System.getenv("DOCKER_PUSH_PASSWORD") ?: ""

rootProject.ext.'deploy.env' = System.getenv("DEPLOY_ENV") ?: "PROD"

// Staging will have random image suffix and default namespace - otherwise project version.
if (rootProject.ext.'deploy.env' != "PROD") {
    rootProject.ext.'deployed.namespace' = stagingNamespaceName
    rootProject.ext.'host.prefix' = stagingHostPrefix
    rootProject.ext.'image.suffix' = "${RandomStringUtils.randomAlphabetic(6).toLowerCase()}".toString()
} else {
    rootProject.ext.'deployed.namespace' = namespaceName
    rootProject.ext.'host.prefix' = ""
    rootProject.ext.'image.suffix' = project.version as String
}

// Local scope props
project.ext {
    setProperty("docker_push_registry", "r3payments.azurecr.io")
    setProperty("frontend_docker_image_name", "${docker_push_registry}/ledger-gallery-frontend")
    setProperty("mock_frontend_docker_image_name", "${docker_push_registry}/ledger-gallery-mock-frontend")
    setProperty("gallery_cordapp_docker_image_name", "${docker_push_registry}/ledger-gallery-cordapp")
    setProperty("gallery_webappapi_docker_image_name", "${docker_push_registry}/ledger-gallery-webappapi")
}

evaluationDependsOn(":gallery-contracts")
evaluationDependsOn(":gallery-workflows")
evaluationDependsOn(":spring-api")

task copyImageSources(type: Copy) {
    dependsOn(project(":gallery-contracts").jar, project(":gallery-workflows").jar, project(":spring-api").bootJar)

    def dockerfilesBase = "$projectDir/azure/dockerfiles"

    // remove prior deploy assets
    doFirst {
        def resourceDir = new File(project.buildDir, "dockerBuild")
        resourceDir.deleteDir()
        resourceDir.mkdir()
    }

    into project.buildDir
    into("dockerBuild/frontend") {
        from("$rootDir/frontend") {
            include '**'
            exclude '*/node_modules/'
        }
        from("$dockerfilesBase/frontend")
    }
    into("dockerBuild/galleryCordapp") {
        from(project(":gallery-contracts").jar.archivePath)
        from(project(":gallery-workflows").jar.archivePath)
        from("$dockerfilesBase/galleryCordapp")
    }
    into("dockerBuild/spring-api") {
        from(project(":spring-api").bootJar.archivePath)
        from("$dockerfilesBase/spring-api")
    }
}

task createAndPushDockerImages(type: ImageTask, dependsOn: 'copyImageSources') {
    description = "Pushes gallery docker images to ACR"
    group = "deployment k8s"
    resourceImageAndVersion = [
            new Triple(new File(project.buildDir, "dockerBuild/frontend"),frontend_docker_image_name, rootProject.ext.'image.suffix'),
            new Triple(new File(project.buildDir, "dockerBuild/frontend"), mock_frontend_docker_image_name, rootProject.ext.'image.suffix'),
            new Triple(new File(project.buildDir, "dockerBuild/galleryCordapp"), gallery_cordapp_docker_image_name, rootProject.ext.'image.suffix'),
            new Triple(new File(project.buildDir, "dockerBuild/spring-api"), gallery_webappapi_docker_image_name, rootProject.ext.'image.suffix')
    ]
    imageToBuildArgsMap = [
            "$frontend_docker_image_name":[
                    "CORDA_ARTIFACTORY_USERNAME": rootProject.ext.'corda.artifactory.username',
                    "CORDA_ARTIFACTORY_PASSWORD": rootProject.ext.'corda.artifactory.password',
                    "REACT_APP_API_HOST":"http://gallery-webappapi.cordapayments-sandbox.com"
            ],
            "$mock_frontend_docker_image_name":[
                    "CORDA_ARTIFACTORY_USERNAME": rootProject.ext.'corda.artifactory.username',
                    "CORDA_ARTIFACTORY_PASSWORD": rootProject.ext.'corda.artifactory.password',
                    "REACT_APP_API_HOST":"http://mock-gallery-webappapi.cordapayments-sandbox.com"
            ]
    ] as Map
}