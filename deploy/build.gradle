import java.util.regex.Pattern

apply plugin: "net.corda.plugins.cordapp"
apply plugin: "net.corda.plugins.cordformation"
apply plugin: 'net.corda.plugins.quasar-utils'
apply from: "./docker.gradle"
apply from: "./azure.gradle"

rootProject.ext.corda_release_version = cordaCoreVersion // required for cordform

dependencies {
    cordaRuntime "$cordaCoreReleaseGroup:corda:$cordaCoreVersion"
    cordaCompile "$cordaCoreReleaseGroup:corda-node-api:$cordaCoreVersion"
    cordaCompile "$tokensReleaseGroup:tokens-contracts:$tokensReleaseVersion"
    cordaCompile "$tokensReleaseGroup:tokens-workflows:$tokensReleaseVersion"

    cordaCompile "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    cordaCompile "org.slf4j:jul-to-slf4j:$slf4jVersion"

    cordapp project(":gallery-contracts")
    cordapp project(":gallery-workflows")
    cordapp "$tokensReleaseGroup:tokens-contracts:$tokensReleaseVersion"
    cordapp "$tokensReleaseGroup:tokens-workflows:$tokensReleaseVersion"
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

String sourceNodeDir = "$project.buildDir/nodes"
String basicNodeDir = "$project.buildDir/BasicNet-Nodes"
String auctionNodeDir = "$project.buildDir/Auction-Nodes"
String gbpNodeDir = "$project.buildDir/GBP-Nodes"
String cbdcNodeDir = "$project.buildDir/CBDC-Nodes"
List<String> allNetworks = [auctionNodeDir, gbpNodeDir, cbdcNodeDir]

/**
 * Create docker-compose for Auction Network - Alice, Bob, Charlie
 */
task prepareAuctionDockerNodes(type: net.corda.plugins.Dockerform, dependsOn: [':gallery-contracts:jar', ':gallery-workflows:jar']) {
    group = "deployment"

    dockerImage = "corda/corda-zulu-java1.8-" + corda_release_version + ":latest"

    def depTargetDir = auctionNodeDir
    directory depTargetDir
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(":gallery-contracts")
        cordapp project(":gallery-workflows")
        cordapp "$tokensReleaseGroup:tokens-contracts:$tokensReleaseVersion"
        cordapp "$tokensReleaseGroup:tokens-workflows:$tokensReleaseVersion"
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
        runSchemaMigration = true
    }

    node {
        name "O=Auction Notary, L=London, C=GB"
        notary = [validating: false]
        p2pPort 10017
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10018")
            adminAddress("0.0.0.0:10019")
        }
        sshdPort 2221
    }

    node {
        name "O=Alice, L=London, C=GB"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10020")
            adminAddress("0.0.0.0:10021")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5000"
                ]
        ]
        sshdPort 2222
    }

    node {
        name "O=Bob, L=San Francisco, C=US"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10023")
            adminAddress("0.0.0.0:10024")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5001"
                ]
        ]
        sshdPort 2223
    }

    node {
        name "O=Charlie, L=Mumbai, C=IN"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10026")
            adminAddress("0.0.0.0:10027")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5002"
                ]
        ]
        sshdPort 2224
    }

    doLast {
        mvYamlToTarget(sourceNodeDir, depTargetDir)
    }
}

/**
 * Create docker-compose for GBP Network - Alice, Bob
 */
task prepareGbpDockerNodes(type: net.corda.plugins.Dockerform, dependsOn: [':gallery-contracts:jar', ':gallery-workflows:jar']) {
    group = "deployment"

    dockerImage = "corda/corda-zulu-java1.8-" + corda_release_version + ":latest"

    def depTargetDir = gbpNodeDir
    directory depTargetDir
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(":gallery-contracts")
        cordapp project(":gallery-workflows")
        cordapp "$tokensReleaseGroup:tokens-contracts:$tokensReleaseVersion"
        cordapp "$tokensReleaseGroup:tokens-workflows:$tokensReleaseVersion"
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
        runSchemaMigration = true
    }

    node {
        name "O=GBP Notary, L=London, C=GB"
        notary = [validating: false]
        p2pPort 10017
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10029")
            adminAddress("0.0.0.0:10030")
        }
        sshdPort 2225
    }

    node {
        name "O=Alice, L=London, C=GB"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10031")
            adminAddress("0.0.0.0:10032")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5003"
                ]
        ]
        sshdPort 2226
    }

    node {
        name "O=Bob, L=San Francisco, C=US"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10034")
            adminAddress("0.0.0.0:10035")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5004"
                ]
        ]
        sshdPort 2227
    }

    doLast {
        mvYamlToTarget(sourceNodeDir, depTargetDir)
    }
}

/**
 * Create docker-compose for CBDC Network - Alice, Charlie
 */
task prepareCbdcDockerNodes(type: net.corda.plugins.Dockerform, dependsOn: [':gallery-contracts:jar', ':gallery-workflows:jar']) {
    group = "deployment"

    dockerImage = "corda/corda-zulu-java1.8-" + corda_release_version + ":latest"

    def depTargetDir = cbdcNodeDir
    directory depTargetDir
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(":gallery-contracts")
        cordapp project(":gallery-workflows")
        cordapp "$tokensReleaseGroup:tokens-contracts:$tokensReleaseVersion"
        cordapp "$tokensReleaseGroup:tokens-workflows:$tokensReleaseVersion"
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
        runSchemaMigration = true
    }

    node {
        name "O=CBDC Notary, L=London, C=GB"
        notary = [validating: false]
        p2pPort 10017
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10037")
            adminAddress("0.0.0.0:10038")
        }
        sshdPort 2228
    }

    node {
        name "O=Alice, L=London, C=GB"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10039")
            adminAddress("0.0.0.0:10040")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"
                ]
        ]
        sshdPort 2229
    }

    node {
        name "O=Charlie, L=Mumbai, C=IN"
        p2pPort 10002
        p2pAddress "0.0.0.0"
        rpcSettings {
            address("0.0.0.0:10042")
            adminAddress("0.0.0.0:10043")
        }
        extraConfig = [
                'custom.jvmArgs': [
                        "-Xmx1G",
                        "-XX:+UseG1GC",
                        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006"
                ]
        ]
        sshdPort 2230
    }

    doLast {
        mvYamlToTarget(sourceNodeDir, depTargetDir)
    }
}

/**
 * Generates all networks
 */
task prepareAllDockerNodes(type: Task, dependsOn: ['prepareAuctionDockerNodes','prepareGbpDockerNodes','prepareCbdcDockerNodes']) {
    group = "deployment"
}

// RUNNERS

task auctionNetworkDown(type: Task) {
    group = "deployment runners"
    def network = auctionNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'down', '--remove-orphans'
        }
    }
}

task auctionNetworkUp(type: Task) {
    group = "deployment runners"
    def network = auctionNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'up', '--remove-orphans', '-d'
        }
    }
}

task gbpNetworkDown(type: Task) {
    group = "deployment runners"
    def network = gbpNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'down', '--remove-orphans'
        }
    }
}

task gbpNetworkUp(type: Task) {
    group = "deployment runners"
    def network = gbpNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'up', '--remove-orphans', '-d'
        }
    }
}

task cbdcNetworkDown(type: Task) {
    group = "deployment runners"
    def network = cbdcNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'down', '--remove-orphans'
        }
    }
}

task cbdcNetworkUp(type: Task) {
    group = "deployment runners"
    def network = cbdcNodeDir

    doLast {
        exec {
            environment "ACCEPT_LICENSE", "YES"
            workingDir network
            commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'up', '--remove-orphans', '-d'
        }
    }
}

/**
 * Convenience task for bringing up all docker networks
 */
task allNetworksUp(type: Task) {
    group = "deployment runners"

    doLast {
        allNetworks.forEach({ network ->
            exec {
                environment "ACCEPT_LICENSE", "YES"
                workingDir network
                commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'up', '--remove-orphans', '-d'
            }
        })
    }
}

/**
 * Convenience task for bringing down all docker networks
 */
task allNetworksDown(type: Task) {
    group = "deployment runners"

    doLast {
        allNetworks.forEach({network ->
            exec {
                environment "ACCEPT_LICENSE", "YES"
                workingDir network
                commandLine 'docker-compose', '-f', "$network/docker-compose.yml", 'down', '--remove-orphans'
            }
        })
    }
}

/**
 * Helper that moves a network/nodes deployment to its own build directory
 * @param source
 * @param target
 * @return
 */
def mvYamlToTarget(String source, String target) {
    // for recognizing a nodes rpc port and injecting a unique debug port locally
    Map<String, String> rpcToDebugPortMap = [
            "10020":"5000", // auction alice
            "10023":"5001", // auction bob
            "10026":"5002", // auction charlie
            "10031":"5003", // gbp alice
            "10034":"5004", // gbp bob
            "10039":"5005", // cbdc alice
            "10042":"5006"  // cbdc charlie
    ]
    File srcFile = new File(source)
    Pattern pattern = Pattern.compile("-\\s(\\d{5})") // rpc port pattern
    copy {
        from(source) {
            include '**/docker-compose.yml'
        }
        into(target)
        filter {
            // Correction of Dockerform incorrect mnt path generations
            line -> line.replaceAll(source, target)
        }
        filter {
            // Makes local ports static rather than generated
            line ->
                def matcher =  pattern.matcher(line as String)

                if (matcher.find()) {
                    def port =  matcher.group(1)
                    def rpcPort = ""
                    if (rpcToDebugPortMap.keySet().contains(port)) {
                        rpcPort = "\n    - \"${rpcToDebugPortMap[port]}:${rpcToDebugPortMap[port]}\""
                    }
                    line.replace(
                        port,
                        "\"${port}:${port}\"$rpcPort"
                    )
                } else line
        }
    }
    srcFile.deleteDir()
}
